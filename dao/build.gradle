plugins {
    id 'java'
    id "com.thinkimi.gradle.MybatisGenerator" version "2.3"
}

mybatisGenerator {
    verbose = true
    configFile = 'src/main/resources/mybatis-generator.xml'

    dependencies {
        mybatisGenerator project(":generator-plugin")
        mybatisGenerator group: 'org.mybatis.generator', name: 'mybatis-generator-core', version: '1.4.1'
        mybatisGenerator group: 'mysql', name: 'mysql-connector-java', version: '8.0.25'
    }
}

dependencies {
    implementation project(":generator-plugin")

    // mybatis
    implementation group: 'org.mybatis.spring.boot', name: 'mybatis-spring-boot-starter'
    implementation group: 'org.mybatis', name: 'mybatis'
    implementation group: 'org.mybatis.generator', name: 'mybatis-generator-core'
    implementation group: 'org.flywaydb', name: 'flyway-core'

    // datasource
    implementation group: 'com.zaxxer', name: 'HikariCP'
    implementation group: 'mysql', name: 'mysql-connector-java'
}

sourceSets {
    main {
        java.srcDirs += ['src/generated/java']
        resources.srcDirs += ['src/generated/resources']
    }
}

tasks.mbGenerator.doFirst {
    def javaDir = "src/generated/java"
    def resourceDir = "src/generated/resources"
    def absoluteJavaDir = projectDir.getAbsolutePath() + "/" + javaDir
    def absoluteResourceDir = projectDir.getAbsolutePath() + "/" + resourceDir

    // 配置环境变量
    System.setProperty("mybatis.generator.baseDir", projectDir.getAbsolutePath())
    System.setProperty("mybatis.generator.javaProjectDir", absoluteJavaDir)
    System.setProperty("mybatis.generator.javaResourceDir", absoluteResourceDir)
    System.setProperty("mybatis.generator.model.package", "cc.cc1234.dao.model")
    System.setProperty("mybatis.generator.mapper.package", "cc.cc1234.dao.mapper")

    // 清理旧文件
    def javaDirFile = file(javaDir)
    if (javaDirFile.exists()) {
        javaDirFile.deleteDir()
        delete(javaDirFile)
    }
    mkdir(javaDirFile)

    def resourceDirFile = file(resourceDir)
    if (resourceDirFile.exists()) {
        delete(resourceDir)
    }
    mkdir(resourceDir)
}